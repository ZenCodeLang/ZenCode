//
// This file is to be applied to every subproject.
//

apply plugin: 'java-library'
apply plugin: 'maven-publish'

String mavenGroupId = 'org.openzen.zencode'
String mavenVersion = '0.3.8'

String branchName = (System.getenv('BRANCH_NAME') ?: 'master').replaceAll('[^A-Za-z-_0-9]', '_')
if(!branchName.equalsIgnoreCase('master')) {
	mavenVersion = branchName + '-' + mavenVersion
}

if (System.getenv('BUILD_NUMBER') != null) {
	mavenVersion += '.' + System.getenv('BUILD_NUMBER')
}

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
}

String mavenArtifactId = name

group = mavenGroupId
version = mavenVersion
task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts {
    archives sourcesJar
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId = mavenGroupId
			artifactId = mavenArtifactId
			version = mavenVersion

			from components.java
		}
	}
	repositories {

		maven {
			url "file://" + System.getenv("local_maven")
		}
	}
}

task createFolders(description: 'Creates the source folders if they do not exist.') doLast {
    sourceSets*.allSource*.srcDirs*.each { File srcDir ->
        if (!srcDir.isDirectory()) {
            println "Creating source folder: ${srcDir}"
            srcDir.mkdirs()
        }
    }
}
